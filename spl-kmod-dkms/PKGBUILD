# Maintainer: Marcel Huber <marcelhuberfoo at gmail dot com>
# Contributor: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=spl-kmod-dkms-git
pkgver=0.6.2.36.g703371d
pkgrel=2
pkgdesc='Solaris Porting Layer kernel modules.'
arch=(i686 x86_64)
groups=(zfsonlinux)
makedepends=(gcc make autoconf automake git)
depends=(dkms)
provides=(spl)
conflicts=(spl-dkms spl-dkms-therp spl-dkms-therp-git spl-kmod-dkms)
url='http://zfsonlinux.org/'
license=(GPL2)
source=($pkgname::git+git://github.com/zfsonlinux/spl.git
        spl-kmod-dkms.install)
sha256sums=('SKIP'
            '95a882ae65fc45cc81d407134c693b5ac267ac2b019dd4a415492172f939193a')
install=spl-kmod-dkms.install
backup=(etc/sysconfig/spl)

pkgver() {
  cd "$srcdir/$pkgname"
  if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
    local _revs_ahead_tag=$(git rev-list --count ${GITTAG}..)
    local _commit_id_short=$(git log -1 --format=%h)
    echo $(sed -e s/^${pkgname%%-git}// -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).${_revs_ahead_tag}.g${_commit_id_short}
  else
    echo 0.$(git rev-list --count master).g$(git log -1 --format=%h)
  fi
}

# $1 version, $2 file
_my_replace_META_Version() {
  local _gitver="${1}"
  local _filename="${2}"
  # replace Version: and Release: in META files
  local _version=$(echo $_gitver | sed 's|\.[0-9]*\.[0-9a-fg]*$||')
  local _release=$(echo $_gitver | sed -r 's|^.*\.([0-9]*\.[0-9a-fg]*)$|\1|')
  sed -i -r -e "/^Version:/ { s/^([^0-9]*)[0-9.]+\$/\1$_version/ }" "$_filename"
  sed -i -r -e "/^Release:/ { s/^([^0-9r]*)[0-9a-fgr.]+\$/\1$_release/ }" "$_filename"
}

prepare() {
  cd "$srcdir/$pkgname"
  _my_replace_META_Version "$(pkgver)" "$srcdir/$pkgname/META"
}

build() {
  cd "$srcdir/$pkgname"
  ./autogen.sh
  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --sbindir=/usr/bin \
              --with-config=user
  scripts/dkms.mkconf -v $pkgver -f dkms.conf -n spl
  # create sysconfig.spl dynamically based on variables from scripts/dkms.mkconf, format is:
  # if [[ \${SPL_DKMS_ENABLE_DEBUG_KMEM,,} == @(y|yes) ]]
  sed -n -r -e 's/^.*\{(SPL_DKMS_ENABLE_[^,]*)[^@]*@\(([^|]*).*$/#\1=\2/ p' scripts/dkms.mkconf > sysconfig.spl
}

package() {
  cd "$srcdir/$pkgname"
  install -d "$pkgdir/usr/src/spl-${pkgver}"
  install -D -m0644 "$srcdir/$pkgname/sysconfig.spl" "$pkgdir/etc/sysconfig/spl"
  cp -a -T "$srcdir/$pkgname/" "$pkgdir/usr/src/spl-${pkgver}"
  rm -rf "$pkgdir/usr/src/spl-${pkgver}/.git"
}

# vim:set ts=2 sw=2 et:
