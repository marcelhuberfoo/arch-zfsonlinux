# Maintainer: Marcel Huber <marcelhuberfoo at gmail dot com>
# Contributor: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=zfs-kmod-dkms-git
pkgver=0.7.0.1596.g2ffd89fcb
pkgrel=1
pkgdesc='Kernel modules for the Zettabyte File System.'
arch=(i686 x86_64)
groups=(zfsonlinux)
makedepends=(gcc make autoconf automake git)
depends=(dkms)
provides=(zfs)
conflicts=(zfs-dkms zfs-dkms-therp zfs-dkms-therp-git zfs-kmod-dkms)
url='http://zfsonlinux.org/'
license=(CDDL)
source=($pkgname::git+git://github.com/zfsonlinux/zfs.git
        zfs-kmod-dkms.install)
sha256sums=('SKIP'
            'fb45023e2f14cb70ad6286c0b73b7346d9e348a6e04d716f0eba4cd943e21c31')
install=zfs-kmod-dkms.install

pkgver() {
  cd "$srcdir/$pkgname" 2>/dev/null && (
  # update dependent projects revision information
  if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
    local _revs_ahead_tag=$(git rev-list --count ${GITTAG}..)
    local _commit_id_short=$(git log -1 --format=%h)
    echo $(sed -e s/^${pkgname%%-git}// -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).${_revs_ahead_tag}.g${_commit_id_short}
  else
    echo 0.$(git rev-list --count master).g$(git log -1 --format=%h)
  fi
  ) || echo $pkgver
}

# $1 version, $2 file
_my_replace_META_Version() {
  local _gitver="${1}"
  local _filename="${2}"
  # replace Version: and Release: in META files
  local _version=$(echo $_gitver | sed 's|\.[0-9]*\.[0-9a-fg]*$||')
  local _release=$(echo $_gitver | sed -r 's|^.*\.([0-9]*\.[0-9a-fg]*)$|\1|')
  sed -i -r -e "/^Version:/ { s/^([^0-9]*)[0-9.]+\$/\1$_version/ }" "$_filename"
  sed -i -r -e "/^Release:/ { s/^([^0-9r]*)[0-9a-fgr.]+\$/\1$_release/ }" "$_filename"
}

prepare() {
  cd "$srcdir/$pkgname"
  git clean -dxf 2>/dev/null
  git reset --hard 2>/dev/null
  _my_replace_META_Version "$(pkgver)" "$srcdir/$pkgname/META"
}

build() {
  cd "$srcdir/$pkgname"
  ./autogen.sh
  scripts/dkms.mkconf -v $pkgver -f dkms.conf -n zfs
}

package() {
  cd "$srcdir/$pkgname"
  install -d "$pkgdir/usr/src/zfs-${pkgver}"
  cp -a -T "$srcdir/$pkgname/" "$pkgdir/usr/src/zfs-${pkgver}"
  rm -rf "$pkgdir/usr/src/zfs-${pkgver}/.git"
}

# vim:set ts=2 sw=2 et:
